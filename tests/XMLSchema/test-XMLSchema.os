Перем ЮнитТест;

#Область ОбработчикиСобытийМодуля

Функция Версия() Экспорт
	Возврат "1.2";
КонецФункции

Функция ПолучитьСписокТестов(МенеджерТестирования) Экспорт
	
	ЮнитТест = МенеджерТестирования;

	СписокТестов = Новый Массив;
	СписокТестов.Добавить("ТестКонструктор");
	СписокТестов.Добавить("TestConstructor");
	СписокТестов.Добавить("ТестУстановитьПространствоИмен");
	СписокТестов.Добавить("ТестФормаАтрибутовПоУмолчанию");
	СписокТестов.Добавить("ТестФормаЭлементовПоУмолчанию");
	СписокТестов.Добавить("ТестОписаниеЭлемента");
	СписокТестов.Добавить("ТестОпределениеПростогоТипа");
	
	Возврат СписокТестов;

КонецФункции

#КонецОбласти

#Область ОбработчикиТестирования

Процедура ТестКонструктор() Экспорт

	СхемаXML = Новый СхемаXML;
	
	ЮнитТест.ПроверитьРавенство(ТипЗнч(СхемаXML), Тип("СхемаXML"));
	
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.Аннотация);
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.Компоненты);
	ЮнитТест.ПроверитьРавенство(СхемаXML.Контейнер, СхемаXML);
	ЮнитТест.ПроверитьРавенство(СхемаXML.КорневойКонтейнер, СхемаXML);
	ЮнитТест.ПроверитьРавенство(СхемаXML.Схема, СхемаXML);
	ЮнитТест.ПроверитьРавенство(СхемаXML.ТипКомпоненты, ТипКомпонентыXS.Схема);
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.ЭлементDOM);

	ЮнитТест.ПроверитьРавенство(СхемаXML.URIПространстваИменСхемыДляСхемыXML, "http://www.w3.org/2001/XMLSchema");
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.Аннотации);
	ЮнитТест.ПроверитьЗаполненность(СхемаXML.БлокировкаПоУмолчанию);
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.Версия);
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.Директивы);
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.ДокументDOM);
	ЮнитТест.ПроверитьЗаполненность(СхемаXML.ЗавершенностьПоУмолчанию);

	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.ОбъявленияАтрибутов);
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.ОбъявленияНотаций);
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.ОбъявленияЭлементов);
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.ОпределенияГруппАтрибутов);
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.ОпределенияГруппМоделей);
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.ОпределенияОграниченийИдентичности);
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.ОпределенияТипов);
	
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.ПрефиксСхемыДляСхемыXML);
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.ПространствоИмен);
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.Содержимое);
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.СхемаДляСхемыXML);

	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.ФормаАтрибутовПоУмолчанию);
	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.ФормаЭлементовПоУмолчанию);

	ЮнитТест.ПроверитьНеЗаполненность(СхемаXML.Язык);

КонецПроцедуры

Procedure TestConstructor() Export

	Schema = New XMLSchema;

	ЮнитТест.ПроверитьРавенство(TypeOf(Schema), Type("XMLSchema"));
	ЮнитТест.ПроверитьРавенство(Schema.ComponentType, XSComponentType.Schema);

EndProcedure

Процедура ТестУстановитьПространствоИмен() Экспорт

	СхемаXML = Новый СхемаXML;
	СхемаXML.ПространствоИмен = "https://oscript.io";

	ЮнитТест.ПроверитьРавенство(СхемаXML.ПространствоИмен, "https://oscript.io");

КонецПроцедуры

Процедура ТестФормаАтрибутовПоУмолчанию() Экспорт

	СхемаXML = Новый СхемаXML;

	СхемаXML.ФормаАтрибутовПоУмолчанию = ФормаПредставленияXS.Неквалифицированная;

	ЮнитТест.ПроверитьРавенство(СхемаXML.ФормаАтрибутовПоУмолчанию, ФормаПредставленияXS.Неквалифицированная);

КонецПроцедуры

Процедура ТестФормаЭлементовПоУмолчанию() Экспорт

	СхемаXML = Новый СхемаXML;

	СхемаXML.ФормаЭлементовПоУмолчанию = ФормаПредставленияXS.Квалифицированная;

	Перем1 = СхемаXML.ФормаЭлементовПоУмолчанию;
	Перем2 = ФормаПредставленияXS.Квалифицированная;

	ЮнитТест.ПроверитьРавенство(СхемаXML.ФормаЭлементовПоУмолчанию, ФормаПредставленияXS.Квалифицированная);

КонецПроцедуры

Процедура ТестОписаниеЭлемента() Экспорт

	СхемаXML = Новый СхемаXML;

	Элемент = Новый ОбъявлениеЭлементаXS;
	Элемент.Имя = "test";
	СхемаXML.Содержимое.Добавить(Элемент);

	ЮнитТест.ПроверитьИстину(СхемаXML.Содержит(Элемент));
	ЮнитТест.ПроверитьРавенство(СхемаXML.Содержимое.Количество(), 1);
	ЮнитТест.ПроверитьРавенство(СхемаXML.Компоненты.Количество(), 1);
	ЮнитТест.ПроверитьРавенство(СхемаXML.ОбъявленияЭлементов.Количество(), 1);
	ЮнитТест.ПроверитьРавенство(Элемент.Контейнер, СхемаXML);
	ЮнитТест.ПроверитьРавенство(Элемент.КорневойКонтейнер, СхемаXML);

КонецПроцедуры

Процедура ТестОпределениеПростогоТипа() Экспорт

	СхемаXML = Новый СхемаXML;

	Определение = Новый ОпределениеПростогоТипаXS;
	Определение.Имя = "test";
	СхемаXML.Содержимое.Добавить(Определение);

	ЮнитТест.ПроверитьИстину(СхемаXML.Содержит(Определение));
	ЮнитТест.ПроверитьРавенство(СхемаXML.Содержимое.Количество(), 1);
	ЮнитТест.ПроверитьРавенство(СхемаXML.Компоненты.Количество(), 1);
	ЮнитТест.ПроверитьРавенство(СхемаXML.ОпределенияТипов.Количество(), 1);
	ЮнитТест.ПроверитьРавенство(Определение.Контейнер, СхемаXML);
	ЮнитТест.ПроверитьРавенство(Определение.КорневойКонтейнер, СхемаXML);

КонецПроцедуры

#КонецОбласти