#Использовать asserts

Перем юТест;
Перем ТекстПроверки;

Функция ПолучитьСписокТестов(Тестирование) Экспорт

	юТест = Тестирование;

	СписокТестов = Новый Массив;
	
	СписокТестов.Добавить("Тест_Должен_СверитьХешСуммуРезультатаПарсингаJSON");
	СписокТестов.Добавить("Тест_Должен_СверитьХешСуммуРезультатаПарсингаJSONПриИспользованииФункцииПропуска");
	СписокТестов.Добавить("Тест_Должен_ПроверитьПолучениеВсехЭлементовПеречисленияТипЗначенияJSON");
	СписокТестов.Добавить("Тест_Должен_СверитьСвойствоСДвойнымиКавычками");
	Возврат СписокТестов;

КонецФункции

Процедура Тест_Должен_ПроверитьПолучениеВсехЭлементовПеречисленияТипЗначенияJSON() Экспорт

юТест.ПроверитьРавенство("Null", Строка(ТипЗначенияJSON.Null));
юТест.ПроверитьРавенство("Комментарий", Строка(ТипЗначенияJSON.Комментарий));
юТест.ПроверитьРавенство("ИмяСвойства", Строка(ТипЗначенияJSON.ИмяСвойства));
юТест.ПроверитьРавенство("КонецМассива", Строка(ТипЗначенияJSON.КонецМассива));
юТест.ПроверитьРавенство("КонецОбъекта", Строка(ТипЗначенияJSON.КонецОбъекта));
юТест.ПроверитьРавенство("НачалоМассива", Строка(ТипЗначенияJSON.НачалоМассива));
юТест.ПроверитьРавенство("НачалоОбъекта", Строка(ТипЗначенияJSON.НачалоОбъекта));
юТест.ПроверитьРавенство("Ничего", Строка(ТипЗначенияJSON.Ничего));
юТест.ПроверитьРавенство("Строка", Строка(ТипЗначенияJSON.Строка));
юТест.ПроверитьРавенство("Число", Строка(ТипЗначенияJSON.Число));

КонецПроцедуры

Процедура Тест_Должен_СверитьСвойствоСДвойнымиКавычками() Экспорт
	
	СтруктураДанных = ПолучитьСтруктуруДанных(Ложь);
		
	юТест.ПроверитьРавенство(СтруктураДанных.lastName, """Иванов""");
	
КонецПроцедуры

Процедура Тест_Должен_СверитьХешСуммуРезультатаПарсингаJSON() Экспорт
	
	СтруктураДанных = ПолучитьСтруктуруДанных(Ложь);
		
	юТест.ПроверитьРавенство(РассчитатьХешСумму(СтруктураДанных), 1289012984);
	
КонецПроцедуры


Процедура Тест_Должен_СверитьХешСуммуРезультатаПарсингаJSONПриИспользованииФункцииПропуска() Экспорт
	
	СтруктураДанных = ПолучитьСтруктуруДанных(Истина);
		
	юТест.ПроверитьРавенство(РассчитатьХешСумму(СтруктураДанных), 3029265435);
	
КонецПроцедуры

Функция РассчитатьХешСумму(СтруктураДанных)

	Текст = "";
	
	Для Каждого ТекСтрока Из СтруктураДанных Цикл
		Если ТипЗнч(ТекСтрока.Значение) = Тип("Структура") Тогда
			Текст = Текст + "Структура - " + ТекСтрока.Ключ  + ":" + Символы.ПС;
			Для Каждого Элемент Из ТекСТрока.Значение Цикл
				Текст = Текст + Элемент.Ключ  + ":" + Элемент.Значение + Символы.ПС;
			КонецЦикла; 
		ИначеЕсли ТипЗнч(ТекСтрока.Значение) = Тип("Массив") Тогда
			Текст = Текст + "Массив - " + ТекСтрока.Ключ  + ":" + Символы.ПС;
			Для Каждого Элемент Из ТекСТрока.Значение Цикл
				Текст = Текст + Элемент + Символы.ПС;
			КонецЦикла; 
		Иначе
		 Текст = Текст + ТекСтрока.Ключ + ":" + ТекСтрока.Значение + Символы.ПС;
		 КонецЕсли;
	 КонецЦикла; 
	 
	Хеширование = Новый ХешированиеДанных(ХешФункция.CRC32);
	Хеширование.Добавить(Текст);
	Возврат Хеширование.ХешСумма;

КонецФункции

Функция ПолучитьСтруктуруДанных(ДобавитьПропуски = Ложь)

	Текст = Новый ТекстовыйДокумент();
	Текст.Прочитать("json\test.json", КодировкаТекста.UTF8);
	
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(Текст.ПолучитьТекст());
	
	Результат = Новый Структура;
		
	Пока Чтение.Прочитать() Цикл
		Если Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.ИмяСвойства Тогда
			ИмяСвойства = Чтение.ТекущееЗначение; 
			Чтение.Прочитать();
			
			Если Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.НачалоОбъекта Тогда
				ВложеннаяСтруктура = Новый Структура;
				Пока Чтение.Прочитать() Цикл
				Если ДобавитьПропуски  Тогда
					Чтение.Пропустить();
				КонецЕсли;
					Если Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.КонецОбъекта Тогда
						Прервать;
					КонецЕсли;
					Если Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.ИмяСвойства Тогда
						ИмяВложенногоСвойства = Чтение.ТекущееЗначение; 
						Чтение.Прочитать();
						
						Если Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.Число Тогда
							ВложеннаяСтруктура.Вставить(ИмяВложенногоСвойства, Формат(Чтение.ТекущееЗначение, "ЧГ=")); 
						ИначеЕсли Чтение.ТипТекущегоЗначения <> ТипЗначенияJSON.НачалоМассива И Чтение.ТипТекущегоЗначения <> ТипЗначенияJSON.НачалоОбъекта Тогда 
							ВложеннаяСтруктура.Вставить(ИмяВложенногоСвойства, Чтение.ТекущееЗначение); 		
						КонецЕсли;				
						
					КонецЕсли;		 
				КонецЦикла; 
				Результат.Вставить(ИмяСвойства, ВложеннаяСтруктура);
				
			ИначеЕсли Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.НачалоМассива Тогда
				Если ДобавитьПропуски  Тогда
					Чтение.Пропустить();
				КонецЕсли;
				ВложенныйМассив = Новый Массив;
				Пока Чтение.Прочитать() Цикл
					Если Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.КонецМассива Тогда
						Прервать;
					КонецЕсли;					
					Если Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.Число Тогда
						ВложенныйМассив.Добавить(Формат(Чтение.ТекущееЗначение, "ЧГ=")); 	
					Иначе
						ВложенныйМассив.Добавить(Чтение.ТекущееЗначение); 		
					КонецЕсли;				
					
				КонецЦикла; 
				Результат.Вставить(ИмяСвойства, ВложенныйМассив);
			Иначе
				Если Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.Число Тогда
					Результат.Вставить(ИмяСвойства, Формат(Чтение.ТекущееЗначение, "ЧГ=")); 	
				Иначе
					Результат.Вставить(ИмяСвойства, Чтение.ТекущееЗначение); 		
				КонецЕсли;				
			КонецЕсли;		
		КонецЕсли;
	КонецЦикла;
	
Возврат Результат;
	
КонецФункции